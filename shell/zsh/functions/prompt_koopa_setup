#!/usr/bin/env zsh

# Koopa ZSH prompt.
# Updated 2019-09-23.



# Notes                                                                     {{{1
# ==============================================================================

# Modified, lighter version of Pure, by Sindre Sorhus.
# https://github.com/sindresorhus/pure

# Debugging:
# > typeset _koopa_prompt_state

# Prompt:
# - %n     => user name
# - %m     => machine (host) name (up to first '.')
# - %M     => machine (host) name (full)
# - %~     => current path
# - %*     => time
# - %(?..) => prompt conditional - %(condition.true.false)
# - %F     => color dict
# - %f     => reset color



_koopa_prompt_precmd() {
    # Always include an initial line break.
    print
    # Warn the user about potential theme conflict.
    if [[ -n "${ZSH_THEME:-}" ]]
    then
        >&2 print "WARNING: Oh My Zsh theme is enabled."
        unset ZSH_THEME
    fi
}



# Conda environment activation is messing up '%m'/'%M' flag on macOS.
# This seems to be specific to Mac and doesn't happen on Linux.

# Set the user string and color based on local or SSH connection.
_koopa_prompt_state_setup() {
    local user wd
    # > user="%n@%m"
    user="${USER}@${HOSTNAME//.*/}"
    if [[ -n "${SSH_CONNECTION:-}" ]] 
    then
        user="%F{$_koopa_prompt_colors[user:ssh]}${user}%f"
    else
        user="%F{$_koopa_prompt_colors[user]}${user}%f"
    fi
    wd='%F{${_koopa_prompt_colors[path]}}%~%f'
    typeset -gA _koopa_prompt_state
    # Details on prompt expansion:
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    # Note that Unicode doesn't work well with some fonts on Windows.
    _koopa_prompt_state+=(
        prompt  "%%"
        user    "$user"
        wd      "$wd"
    )
}



_koopa_prompt_setup() {
    setopt localoptions noshwordsplit
    prompt_opts=(subst percent)

    # This variable needs to be set, usually set by promptinit.
    if [[ -z "${prompt_newline:-}" ]]
    then
        typeset -g prompt_newline=$'\n%{\r%}'
    fi
    
    # Set the colors.
    typeset -gA _koopa_prompt_colors_default _koopa_prompt_colors
    _koopa_prompt_colors_default=(
        path                 blue
        prompt:error         red
        prompt:success       magenta
        user                 cyan
        user:ssh             yellow
        virtualenv           default
    )
    _koopa_prompt_colors=("${(@kv)_koopa_prompt_colors_default}")

    # Run pre commeands.
    add-zsh-hook precmd _koopa_prompt_precmd
    
    # Set up the prompt variables.
    _koopa_prompt_state_setup

    # Construct the new prompt with a clean preprompt.
    # Join parts, space separated.
    local -ah header ps1
    
    # Note that use of koopa causes bash to spawn here.
    header=(
        "${_koopa_prompt_state[user]}"
        "\$(koopa prompt-conda-env)"
        "\$(koopa prompt-python-env)"
    )
    header="${(j..)header}"

    ps1=(
        "$header"
        "$prompt_newline"
        "${_koopa_prompt_state[wd]}"
        "$prompt_newline"
    )

    # Coerce array to string.
    PROMPT="${(j..)ps1}"

    # Prompt turns red if the previous command didn't exit with 0.
    PROMPT+='%(?.%F{$_koopa_prompt_colors[prompt:success]}.%F{$_koopa_prompt_colors[prompt:error]})${_koopa_prompt_state[prompt]}%f '

    # Guard against Oh My Zsh themes overriding the prompt.
    unset -v ZSH_THEME
}



_koopa_prompt_setup "$@"
